#lang racket/base

;;;
;;; define-accumulator.rkt
;;;
;;; Provides define-accumulator, an interface for creating new accumulators.
;;;

(require (for-syntax racket/base
                     racket/syntax
                     syntax/parse
                     "accumulator-syntax-classes.rkt"
                     "iterator-syntax-classes.rkt"
                     "common-syntax-classes.rkt"))

(provide (all-defined-out))

(define-syntax (define-accumulator stx)
  (syntax-parse stx
    [(_ name:id
        [(pattern-directive ...)
         (~alt (~seq #:declare declare-pattern-id declare-stxclass
                     (~optional (~seq #:role declare-role-expr)))
               (~seq #:post post-action-pattern)
               (~seq #:with with-syntax-pattern with-expr)
               (~seq #:fail-when fail-when-condition-expr fail-when-message-expr)
               (~seq #:fail-unless fail-unless-condition-expr fail-unless-message-expr)
               (~seq #:when when-condition-expr)
               (~seq #:do [do-def-or-expr ...])
               (~seq #:undo [undo-def-or-expr ...]))
         ...
         outer-bindings:outer-bindings-directive
         outer-checks:outer-checks-directive
         initial-arguments:initial-arguments-directive
         pos-guard:pos-guard-directive
         pos-done:pos-done-directive
         inner-bindings:inner-bindings-directive
         pre-guard:pre-guard-directive
         pre-done:pre-done-directive
         body-results:body-results-directive
         body-bindings:body-bindings-directive
         post-guard:post-guard-directive
         post-done:post-done-directive
         loop-arguments:loop-arguments-directive
         return:return-directive]
        ...+)
     #:with to-name (format-id #'name "to-~a" #'name)
     #:with for/name (format-id #'name "for/~a" #'name)
     #'(begin
         (define-syntax (to-name stx)
           (syntax-parse stx
             [(pattern-directive ...)
              (~@ #:declare declare-pattern-id declare-stxclass
                  (~? (~@ #:role declare-role-expr))) ...
              (~@ #:post post-action-pattern) ...
              (~@ #:with with-syntax-pattern with-expr) ...
              (~@ #:fail-when fail-when-condition-expr fail-when-message-expr) ...
              (~@ #:fail-unless fail-unless-condition-expr fail-unless-message-expr) ...
              (~@ #:when when-condition-expr) ...
              (~@ #:do [do-def-or-expr ...]) ...
              (~@ #:undo [undo-def-or-expr ...]) ...
              #`(outer-bindings.outer-bindings
                 outer-checks.outer-checks
                 initial-arguments.initial-arguments
                 pos-guard.pos-guard
                 (~? pos-done.pos-done return.return)
                 inner-bindings.inner-bindings
                 pre-guard.pre-guard
                 (~? pre-done.pre-done
                     (~? pos-done.pos-done return.return))
                 body-results.body-results
                 body-bindings.body-bindings
                 post-guard.post-guard
                 (~? post-done.post-done
                     (~? pos-done.pos-done
                         (~? pre-done.pre-done return.return)))
                 loop-arguments.loop-arguments)]
             ...))
         (... (define-syntax (for/name stx)
                (syntax-parse stx
                  [(_ argument ...
                      ([match-pattern ...+ iterator:iterator] ...)
                      body ...+)
                   #'(for (to-name argument ...) ([match-pattern ... iterator] ...)
                       body ...)]))))]))
